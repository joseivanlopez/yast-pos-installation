/**
 * This client should be called at the beginning of update with SLESPOS 11 Add-On,
 * to silently perform some initialization steps before the real update starts
 */

{
    import "Directory";
    import "GetInstArgs";
    import "Mode";
    import "Installation";
    import "Stage";
    import "String";

    y2milestone ("SLEPOS initializaton (%1, %2)", Mode::mode (), Stage::stage ());

    if (!Mode::update () || !Stage::initial ())
    {
	y2milestone ("this should only run in first stage of update: exiting");
	return `auto;
    }

    // save old /etc/SuSE-release, so we can find it in 2nd stage
    // it will be in /root/inst-sys/imported after installation
    if (!GetInstArgs::going_back ())
    {
	SCR::Execute (.target.mkdir, "/root/imported");
	map out = (map) SCR::Execute (.target.bash_output, sformat (
	    "cp '%1/etc/SuSE-release' /root/imported",
	    String::Quote (Installation::destdir))
	);
	if (out["exit"]:1 != 0)
	{
	    y2error ("saving old SuSE-release failed: %1", out);
	}
	else
	    y2milestone ("SuSE-release copied to /root/imported");
    }
    return `auto;
}
